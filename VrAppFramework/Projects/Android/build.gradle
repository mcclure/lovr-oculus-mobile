apply plugin: 'com.android.library'

android {
	compileSdkVersion 26
	buildToolsVersion '27.0.3'

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 23

		externalNativeBuild {
			ndk {
				abiFilters 'arm64-v8a'
			}
			ndkBuild {
				targets "vrappframework"

				def numProcs = Runtime.runtime.availableProcessors()
				arguments "V=0", "-j$numProcs", "-C$project.buildDir.parent"
				abiFilters 'arm64-v8a'
			}
		}
	}

	externalNativeBuild {
		ndkBuild {
			path 'jni/Android.mk'
		}
	}

	buildTypes {
		debug {
			debuggable true
			jniDebuggable true

			externalNativeBuild {
				ndkBuild {
					arguments "NDK_DEBUG=1","OVR_DEBUG=1","USE_ASAN=1"
				}
			}
		}

		release {
			debuggable false
			jniDebuggable false

			externalNativeBuild {
				ndkBuild {
					arguments "NDK_DEBUG=0","OVR_DEBUG=0","USE_ASAN=0"
				}
			}
		}
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['../../java']
			res.srcDirs = ['../../res']
			// Native libs will be picked up by the build scripts.  They are not
			// included in the aar because the format does not allow for header
			// files or other ways to link native code against any binaries
			// within.
		}
	}
}

task( "cleanWorkAround" ) {
    description "Workaround for .externalNativeBuild not being deleted on clean"
} << {
    project.delete project.file( ".externalNativeBuild" )
}

// WORKAROUND: On Mac OS X, running ndk-build clean with a high num of parallel executions
// set may result in the following build error:  rm: fts_read: No such file or directory.
// Currently, there isn't a good way to specify numProcs=1 only on clean. So, in order
// to work around the issue, delete the auto-generated .externalNativeBuild artifacts
// (where $numProcs specified) before executing the clean task.
clean.dependsOn cleanWorkAround
